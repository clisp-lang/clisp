dnl AUTOCONF configuration for libsvm
dnl Sam Steingold 2010

AC_PREREQ(2.57)
AC_INIT(libsvm, 1.0, clisp-list)
AC_CONFIG_SRCDIR(libsvm.lisp)
AC_CONFIG_HEADERS(config.h)

RSE_BOLD
BOLD_MSG([LibSVM (Common)])
CL_MODULE_COMMON_CHECKS([ffi])

dnl Search for libsvm and define LIBSVM, LTLIBSVM and INCSVM.
AC_LIB_LINKFLAGS([svm])

BOLD_MSG([LibSVM (Headers)])
dnl On ubuntu libsvm is managed by pkg-config which seems rather silly
dnl because there is no real connection between gnome and libsvm.
dnl We must support both pkg-config and non-pkg-config installations!
PKG_CHECK_MODULES([LIBSVM],[libsvm],
[AC_LIB_APPENDTOVAR([CPPFLAGS],[$LIBSVM_CFLAGS])],
[echo ignore pkg-config errors])
AC_SUBST(LIBSVM_CFLAGS)
AC_SUBST(LIBSVM_LIBS)
AC_CHECK_HEADERS(svm.h)
if test "$ac_cv_header_svm_h" = "no"; then
  AC_MSG_ERROR([cannot find LibSVM headers])
fi

BOLD_MSG([LibSVM (Functions)])
AC_LIB_APPENDTOVAR([LIBS], [$LIBSVM])
AC_SEARCH_LIBS(svm_train, svm)
if test "$ac_cv_search_svm_train" = "no"; then
  AC_MSG_ERROR([cannot find LibSVM library])
fi
AC_CHECK_FUNCS(svm_check_parameter dnl
svm_check_probability_model dnl
svm_cross_validation dnl
svm_destroy_model dnl
svm_destroy_param dnl
svm_get_labels dnl
svm_get_nr_class dnl
svm_get_svm_type dnl
svm_get_svr_probability dnl
svm_load_model dnl
dnl svm_predict
svm_predict_probability dnl
svm_predict_values dnl
svm_save_model dnl
svm_train dnl
)

BOLD_MSG([Libsvm (Output)])
AC_CONFIG_FILES(Makefile link.sh)
AC_OUTPUT

BOLD_MSG([Libsvm (Done)])
